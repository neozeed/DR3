/*
 * Copyright 1991-1998 by Open Software Foundation, Inc. 
 *              All Rights Reserved 
 *  
 * Permission to use, copy, modify, and distribute this software and 
 * its documentation for any purpose and without fee is hereby granted, 
 * provided that the above copyright notice appears in all copies and 
 * that both the copyright notice and this permission notice appear in 
 * supporting documentation. 
 *  
 * OSF DISCLAIMS ALL WARRANTIES WITH REGARD TO THIS SOFTWARE 
 * INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS 
 * FOR A PARTICULAR PURPOSE. 
 *  
 * IN NO EVENT SHALL OSF BE LIABLE FOR ANY SPECIAL, INDIRECT, OR 
 * CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM 
 * LOSS OF USE, DATA OR PROFITS, WHETHER IN ACTION OF CONTRACT, 
 * NEGLIGENCE, OR OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION 
 * WITH THE USE OR PERFORMANCE OF THIS SOFTWARE. 
 */
/*
 * MkLinux
 */
/* CMU_HIST */
/*
 * Revision 2.6  91/12/13  13:45:12  jsb
 * 	Moved non-exported interfaces to norma/norma_internal.defs.
 * 	Changed name of task_create_remote to norma_task_create.
 * 	Added comments.
 * 
 * Revision 2.5  91/11/14  16:56:43  rpd
 * 	Picked up mysterious norma changes.
 * 	[91/11/14            rpd]
 * 
 * Revision 2.4  91/08/28  11:15:08  jsb
 * 	Added KERNEL_USER definitions.
 * 	Removed norma_set_task_server.
 * 	Added task_create_remote, norma_copy_create.
 * 	[91/08/15  13:28:27  jsb]
 * 
 * Revision 2.3  91/08/03  18:19:02  jsb
 * 	Removed norma_get_{host,host_priv,device}_port;
 * 	Use norma_{get,set}_special_port instead.
 * 	[91/07/25  07:51:11  jsb]
 * 
 * Revision 2.2  91/06/06  17:07:57  jsb
 * 	First checkin.
 * 	[91/05/25  10:37:22  jsb]
 * 
 */
/* CMU_ENDHIST */
/* 
 * Mach Operating System
 * Copyright (c) 1991 Carnegie Mellon University
 * All Rights Reserved.
 * 
 * Permission to use, copy, modify and distribute this software and its
 * documentation is hereby granted, provided that both the copyright
 * notice and this permission notice appear in all copies of the
 * software, derivative works or modified versions, and any portions
 * thereof, and that both notices appear in supporting documentation.
 * 
 * CARNEGIE MELLON ALLOWS FREE USE OF THIS SOFTWARE IN ITS "AS IS"
 * CONDITION.  CARNEGIE MELLON DISCLAIMS ANY LIABILITY OF ANY KIND FOR
 * ANY DAMAGES WHATSOEVER RESULTING FROM THE USE OF THIS SOFTWARE.
 * 
 * Carnegie Mellon requests users of this software to return to
 * 
 *  Software Distribution Coordinator  or  Software.Distribution@CS.CMU.EDU
 *  School of Computer Science
 *  Carnegie Mellon University
 *  Pittsburgh PA 15213-3890
 * 
 * any improvements or extensions that they make and grant Carnegie Mellon
 * the rights to redistribute these changes.
 */
/*
 */

subsystem
#if	KERNEL_USER
	  KernelUser
#endif	KERNEL_USER
#if	KERNEL_SERVER
	  KernelServer
#endif	KERNEL_SERVER
		       mach_norma 555000;
#ifdef	KERNEL_USER
userprefix r_;
#endif	KERNEL_USER

#include <mach/std_types.defs>
#include <mach/mach_types.defs>

skip;

/*
 * Specify a node upon which children tasks will be created.
 * This call exists only to allow testing with unmodified servers.
 * Server developers should use norma_task_create instead.
 */
routine	task_set_child_node(
		target_task	: task_t;
		child_node	: int);

/*
 * THIS CALL WILL BE ELIMINATED.
 * Use norma_port_location_hint(,mach_task_self(),) instead.
 */
routine norma_node_self(
		host		: host_t;
	out	node		: int);

skip;

skip;

/*
 * Create a task on the given node, possibly inheriting memory.
 * Same inheritance semantics as task_create, including inheritance
 * of initial ports and emulation library, except for memory:
 * inheritance attributes are ignored, so that all regions appear
 * in the child task, shared with the parent, until the parent
 * is destroyed. (The inheritance of the regions in the child
 * will, however, be set to match the parent.)
 *
 * This call is intended to support process migration, where the
 * inheritance semantics of norma_task_create would break migrated
 * programs that depended upon sharing relationships remaining
 * after migration.
 *
 * This call is not a true task migration call, in that it does not
 * migrate the port space, threads, and other non-address-space
 * attributes of the task.
 */
routine norma_task_clone(
		target_task	: task_t;
		inherit_memory	: boolean_t;
		child_node	: int;
	out	child_task	: task_t);

/*
 * Create a task on the given node, possibly inheriting memory.
 * Same inheritance semantics as task_create, including inheritance
 * of initial ports and emulation library.
 * Setting child_node to node_self forces local task creation.
 */
routine norma_task_create(
		target_task	: task_t;
		inherit_memory	: boolean_t;
		child_node	: int;
	out	child_task	: task_t);

/*
 * Get a given special port for a given node.
 * Norma special ports are defined in norma_special_ports.h;
 * examples include the master device port.
 * There are a limited number of slots available for system servers.
 */
routine	norma_get_special_port(
		host_priv	: host_priv_t;
		node		: int;
		which		: int;
	out	port		: mach_port_t);

/*
 * Set a given special port for a given node.
 * See norma_get_special_port.
 */
routine	norma_set_special_port(
		host_priv	: host_priv_t;
		which		: int;
		port		: mach_port_t);

/*
 * Just like norma_task_clone, except target_task is terminated,
 * allowing useful VM optimizations.
 */
routine norma_task_teleport(
		target_task	: task_t;
		inherit_memory	: boolean_t;
		child_node	: int;
	out	child_task	: task_t);

skip;

skip;

/*
 * Return best guess of port's current location.
 * Guaranteed to be a node where the port once was.
 * Guaranteed to be accurate if port has never moved.
 * Can be used to determine residence node for hosts, tasks, threads, etc.
 */
routine norma_port_location_hint(
		task		: task_t;
		port		: mach_port_t;
	out	node		: int);
